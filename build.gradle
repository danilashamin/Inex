// Top-level build file where you can add configuration options common to all
// sub-projects/modules.
buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.2.0"
    }
}

apply plugin: 'com.android.application'

repositories {
    google()
    jcenter()
    mavenCentral()
}

configurations {
    playstoreCompile
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

dependencies {
    implementation project(':libs:MemorizingTrustManager')
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.google.android.gms:play-services-gcm:15.0.1'
    implementation 'org.sufficientlysecure:openpgp-api:10.0'
    implementation 'com.soundcloud.android:android-crop:1.0.1@aar'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v13:28.0.0'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.56'
    implementation 'org.bouncycastle:bcmail-jdk15on:1.52'
    implementation 'org.jitsi:org.otr4j:0.22'
    implementation 'org.gnu.inet:libidn:1.15'
    implementation 'com.google.zxing:core:3.3.3'
    implementation 'com.google.zxing:android-integration:3.2.1'
    implementation 'de.measite.minidns:minidns:0.1.7'
    implementation 'de.timroes.android:EnhancedListView:0.3.4'
    implementation 'me.leolin:ShortcutBadger:1.1.13@aar'
    implementation 'com.kyleduo.switchbutton:library:1.2.8'
    implementation 'org.whispersystems:axolotl-android:1.3.4'
    implementation 'com.makeramen:roundedimageview:2.3.0'
    implementation 'com.wefika:flowlayout:0.4.1'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'net.ypresto.androidtranscoder:android-transcoder:0.2.0'
    //compile 'de.hdodenhof:circleimageview:1.1.0'
}

ext {
    travisBuild = System.getenv("TRAVIS") == "true"
    // allows for -Dpre-dex=false to be set
    preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.2'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 12736
        versionName "1.12.26-kav"
        archivesBaseName += "-$versionName"
        applicationId "ru.messenger.inex_messenger"
        multiDexEnabled true
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    productFlavors {
        flavorDimensions "default"
        playstore
        free
        free {
            minSdkVersion 16
            targetSdkVersion 27
        }
        playstore {
            minSdkVersion 16
            targetSdkVersion 27
        }
    }
    if (hasProperty('mStoreFile') &&
            hasProperty('mStorePassword') &&
            hasProperty('mKeyAlias') &&
            hasProperty('mKeyPassword')) {
        signingConfigs {
            release {
                storeFile file(mStoreFile)
                storePassword mStorePassword
                keyAlias mKeyAlias
                keyPassword mKeyPassword
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }

    lintOptions {
        disable 'ExtraTranslation', 'MissingTranslation', 'InvalidPackage', 'MissingQuantity', 'AppCompatResource'
    }

    subprojects {

        afterEvaluate {
            if (getPlugins().hasPlugin('android') ||
                    getPlugins().hasPlugin('android-library')) {

                configure(android.lintOptions) {
                    disable 'AndroidGradlePluginVersion', 'MissingTranslation'
                }
            }

        }
    }

    packagingOptions {
        exclude 'META-INF/BCKEY.DSA'
        exclude 'META-INF/BCKEY.SF'
    }
    useLibrary 'org.apache.http.legacy'
}
